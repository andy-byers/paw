-- mandelbrot.paw
-- usage: paw mandelbrot.paw -- x0 x1 nx y0 y1 ny
-- translated from https://rosettacode.org/wiki/Mandelbrot_set#ASCII_2 (Lua).

let io = require('io')
let file = io.stdout

let charmap = [
    ' ', '.', ':', '-', '=', 
    '+', '*', '#', '%', '@',
]

let x0 = float(argv[0]) 
let x1 = float(argv[1]) 
let nx = int(argv[2]) 
let y0 = float(argv[3]) 
let y1 = float(argv[4]) 
let ny = int(argv[5]) 

assert(nx > 0 && ny > 0)
let dy = (y1 - y0) / (ny - 1) 
let dx = (x1 - x0) / (nx - 1) 

for i = 0, ny {
    let y = y0 + i * dy
    for j = 0, nx {
        let x = x0 + j * dx
        let zi = 0
        let zr = 0
        let i = 0
        while i < 100 {
            if zi*zi+zr*zr >= 4 {
                break
            }
            let zz = zr*zr - zi*zi + x
            zi = 2*zr*zi + y
            zr = zz
            i = i + 1
        }
        io.write(file, charmap[i % 10])
    }
    io.write(file, '\n')
}
io.flush(file)
