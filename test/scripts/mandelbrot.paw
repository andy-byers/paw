// mandelbrot.paw
// usage: paw mandelbrot.paw x0,x1,nx,y0,y1,ny
// translated from https://rosettacode.org/wiki/Mandelbrot_set#ASCII_2 (Lua).

//let io = require('io')
//let file = io.stdout

pub fn main(args: [str]) -> int {
    let charmap = [
        ' ', '.', ':', '-', '=',
        '+', '*', '#', '%', '@',
    ];

    let values = [];
    if #args > 0 {
        values = _string_split(args[0], ',');
    } else {
        values = ['-2.1', '0.9', '80', '-1.3', '1.3', '24'];
    };
    let x0 = _string_parse_float(values[0]);
    let x1 = _string_parse_float(values[1]);
    let nx = _string_parse_int(values[2], 10);
    let y0 = _string_parse_float(values[3]);
    let y1 = _string_parse_float(values[4]);
    let ny = _string_parse_int(values[5], 10);

    assert(nx > 0 && ny > 0);
    let dy = (y1 - y0) / (ny - 1) as float;
    let dx = (x1 - x0) / (nx - 1) as float;

    for i = 0, ny {
        let y = y0 + i as float * dy;
        for j = 0, nx {
            let x = x0 + j as float * dx;
            let zi = 0.0;
            let zr = 0.0;
            let i = 0;
            while i < 100 {
                if zi*zi+zr*zr >= 4.0 {
                    break;
                };
                let zz = zr*zr - zi*zi + x;
                zi = 2.0*zr*zi + y;
                zr = zz;
                i = i + 1;
            };
            print(charmap[i % 10]);
        };
        print('\n');
    };
    return 0;
}
