// mandelbrot.paw
// usage: paw mandelbrot.paw x0,x1,nx,y0,y1,ny
// translated from https://rosettacode.org/wiki/Mandelbrot_set#ASCII_2 (Lua).

//mod mandelbrot
//use io

pub fn main(args: [str]) -> int {
    let charmap = [
        ' ', '.', ':', '-', '=',
        '+', '*', '#', '%', '@',
    ];

    let values = [];
    if #args > 0 {
        values = args[0].split(',');
    } else {
        values = ['-2.1', '0.9', '80', '-1.3', '1.3', '24'];
    }
    let x0 = values[0].parse_float();
    let x1 = values[1].parse_float();
    let nx = values[2].parse_int(10);
    let y0 = values[3].parse_float();
    let y1 = values[4].parse_float();
    let ny = values[5].parse_int(10);

    assert(nx > 0 && ny > 0);
    let dy = (y1 - y0) / (ny - 1) as float;
    let dx = (x1 - x0) / (nx - 1) as float;

    for i = 0, ny {
        let y = y0 + i as float * dy;
        for j = 0, nx {
            let x = x0 + j as float * dx;
            let zi = 0.0;
            let zr = 0.0;
            let i = 0;
            while i < 100 {
                if zi*zi+zr*zr >= 4.0 {
                    break;
                }
                let zz = zr*zr - zi*zi + x;
                zi = 2.0*zr*zi + y;
                zr = zz;
                i = i + 1;
            }
            print(charmap[i % 10]);
        }
        print('\n');
    }
    return 0;
}
